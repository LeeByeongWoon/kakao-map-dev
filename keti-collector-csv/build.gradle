plugins {
	id 'java'
	id "com.moowork.node" version "1.3.1"
	id "com.moowork.gulp" version "1.3.0"
	id 'application'
	id 'org.springframework.boot' version '2.4.9'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.keti.collector'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
mainClassName= 'com.keti.collector.KetiCollectorCsvApplication'

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
			srcDirs = ["src/main/resources"]
		}
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: '2.5.2'
	implementation group: 'org.springframework.boot', name: 'spring-boot-devtools', version: '2.5.2'
	implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4'
	implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
	implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'
	implementation group: 'com.github.miwurster', name: 'spring-data-influxdb', version: '1.8'
    implementation 'org.influxdb:influxdb-java:2.20'
	implementation group: 'org.mongodb', name: 'bson', version: '4.3.3'
	implementation group: 'org.mongodb', name: 'mongodb-driver-core', version: '4.3.3'
	implementation group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.3.3'
	implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.3'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.3'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.12.3'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation "io.springfox:springfox-boot-starter:3.0.0"


	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
    String springProfilesActive = System.properties['spring.profiles.active']
    systemProperty "spring.profiles.active", springProfilesActive
}

task npm_start(type: NpmTask) {
	workingDir = file("src/main/webapp")
	args = ["start"]
}

task npm_install(type: NpmTask) {
	workingDir = file("src/main/webapp")
	args = ["install"]
}

task npm_build(type: NpmTask) {
	workingDir = file("src/main/webapp")
	args = ["run", "build"]
	// script = file("src/main/webapp/scripts/build.js")
}

task copy_build(type: Copy) {
	from file("src/main/webapp/build/.")
	into file("src/main/resources/static")
}

task copy_index(type: Copy) {
	from file("src/main/webapp/build/index.html")
	into file("src/main/resources/templates")
}

task copy_main(type: Copy) {
	from file("src/main/webapp/build/index.html")
	into file("src/main/resources/templates")
	rename { String name -> name.replace("index.html", "main.html") }
}


npm_build.dependsOn npm_install
copy_build.dependsOn npm_build
copy_index.dependsOn copy_build
copy_main.dependsOn copy_index
compileJava.dependsOn copy_main

// test {
// 	useJUnitPlatform()
// }
